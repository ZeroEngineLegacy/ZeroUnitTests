class CollisionTableTester : ZilchComponent
{
  [Property] var Active : Boolean = true;
  
  [Dependency] var Collider : Collider;
  
  [Property] var CollisionTable : CollisionTable = CollisionTable.TestTable;
  
  function Initialize(init : CogInitializer)
  {
    if(!this.Active)
      return;
      
    Zero.Connect(this.Owner, Events.RecordInitialization, this.OnRecordInitialization);
  }
  
  function TestRuntimeClone(stream : UnitTestStreamWriter)
  {
    stream.WriteLine("ClonedTable:");
    stream.BeginScope();

    var clone = this.CollisionTable.RuntimeClone();
    this.WriteTable(stream, clone);
    
    stream.EndScope();
  }
  
  function TestCreateRuntime(stream : UnitTestStreamWriter)
  {
    stream.WriteLine("CreateTable:");
    stream.BeginScope();

    var table = CollisionTable.CreateRuntime();
    this.WriteTable(stream, table);
    
    stream.EndScope();
  }

  function OnRecordInitialization(event : CogFrameRecordEvent)
  {
    var stream = event.Stream;
    var table = this.CollisionTable;
    
    this.WriteTable(stream, table);
    
    this.TestCreateRuntime(stream);
    this.TestRuntimeClone(stream);
    
    stream.WriteLine("Original Table:");
    this.WriteTable(stream, table);
  }
  
  function WriteTable(stream : UnitTestStreamWriter, table : CollisionTable)
  {
    stream.WriteLine("CollisionTable:");
    stream.BeginScope();
    
    stream.WriteLine(@table.Name);
    this.WriteFilter(stream, table, CollisionGroup.GroupA, CollisionGroup.GroupB);
    this.WriteFilter(stream, table, CollisionGroup.GroupA, CollisionGroup.GroupA);
    this.WriteFilter(stream, table, CollisionGroup.GroupB, CollisionGroup.GroupB);
    
    stream.EndScope();
  }
  
  function WriteFilter(stream : UnitTestStreamWriter, table : CollisionTable, groupA : CollisionGroup, groupB : CollisionGroup)
  {
    var filter = table.FindFilter(groupA, groupB);
    if(filter != null)
      filter.WriteToStream(stream);
  }
}

class CollisionFilterExtensions
{
  [Extension(typeid(CollisionFilterBlock))]
  function WriteToStream(stream : UnitTestStreamWriter)
  {
    stream.WriteLine("CollisionFilterBlock: `this.BlockType`");
    stream.BeginScope();
    
    stream.WriteLine(@this.SendEventsToA);
    stream.WriteLine(@this.SendEventsToB);
    stream.WriteLine(@this.SendEventsToSpace);
    stream.WriteLine(@this.EventOverride);
    
    stream.EndScope();
  }
  
  [Extension(typeid(CollisionFilter))]
  function WriteToStream(stream : UnitTestStreamWriter)
  {
    stream.WriteLine("CollisionFilter:  (`this.CollisionGroupA.Name` / `this.CollisionGroupB.Name`)");
    stream.BeginScope();
    
    stream.WriteLine("CollisionFlags: `this.CollisionFlag`");
    if(this.CollisionStartBlock != null)
      this.CollisionStartBlock.WriteToStream(stream);
    if(this.CollisionPersistedBlock != null)
      this.CollisionPersistedBlock.WriteToStream(stream);
    if(this.CollisionEndBlock != null)
      this.CollisionEndBlock.WriteToStream(stream);
    if(this.PreSolveBlock != null)
      this.PreSolveBlock.WriteToStream(stream);
    
    stream.EndScope();
  }
}
